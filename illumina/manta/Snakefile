
import os
import shutil
from collections import defaultdict


def get_threads(rule, default):
    cluster_config = snakemake.workflow.cluster_config
    if rule in cluster_config and "threads" in cluster_config[rule]:
        return cluster_config[rule]["threads"]
    if "default" in cluster_config and "threads" in cluster_config["default"]:
        return cluster_config["default"]["threads"]
    return default


def get_mem(rule, default):
    cluster_config = snakemake.workflow.cluster_config
    if rule in cluster_config and "mem" in cluster_config[rule]:
        return cluster_config[rule]["mem"]
    if "default" in cluster_config and "mem" in cluster_config["default"]:
        return cluster_config["default"]["mem"]
    return default


def get_bams(sample_file):
    with open(sample_file) as fin:
        bams = [os.path.abspath(line.rstrip()) for line in fin]
    return bams


bamfiles = get_bams(config['sample_file'])
reference = os.path.abspath(config['reference'])

workdir: config['workdir']

rootmanta = "/home/faraut/dynawork/CNVPipeline/bacAsable/manta/manta-1.6.0.centos6_x86_64"
manta_bin = rootmanta + "/bin"

svtypes = ['DEL', 'INS', 'INV', 'DUP']

shell.prefix("export PATH=%s:$PATH;" % manta_bin)

configManta = manta_bin + "/configManta.py"

localrules: regions

rule all:
    input:
        expand("manta_{svtype}.vcf.gz", svtype=svtypes)
    output:
        touch("done.txt")
    shell:
        """
        tar -zcvf rundir.tar.gz rundir
        rm -fr rundir
        rm -fr logs
        """

rule convert:
    input:
        vcf = "rundir/results/variants/diploidSV.vcf.gz",
        reference = reference,
    output:
        "rundir/results/variants/diploidSVconverted.vcf.gz"
    params:
        convertinv = rootmanta + "/libexec/convertInversion.py",
        samtoolsexe = shutil.which("samtools")
    shell:
        "{params.convertinv} {params.samtoolsexe} {input.reference} {input.vcf}"
        " | bcftools sort -Oz -o {output}"


rule configmanta:
    input:
        bams = bamfiles,
        reference = reference,
        fai = reference+".fai",
        regions = "regions.bed.gz"
    output:
        mantarunner = "rundir/runWorkflow.py"
    params:
        rundir = "rundir"
    log:
        stdout = "logs/config.o",
        stderr = "logs/config.e"
    threads:
        1
    run:
        from subprocess import run
        command = configManta + " "
        for bam in input.bams:
            command += "--bam %s " % bam
        command += "--referenceFasta %s " % input.reference
        command += "--runDir %s " % params.rundir
        command += "--callRegions %s " % input.regions
        print(command)
        result = run(command, shell=True)

rule regions:
    input:
        reference = reference,
        fai = reference+".fai"
    output:
        "regions.bed.gz.tbi",
        region = "regions.bed.gz"
    params:
        chromregex = config['chromregex']
    shell:
        "cat {input.fai} | egrep \'^{params.chromregex}\' "
        " | awk -v OFS='\\t' '{{ print $1,0,$2}}' | bgzip -c > {output.region}; "
        "tabix {output.region}"

rule runmanta:
    input:
        mantarunner = "rundir/runWorkflow.py"
    output:
        "rundir/results/variants/diploidSV.vcf.gz"
    threads:
        get_threads("runmanta", 5)
    params:
        mem = get_mem("runmanta", 24)
    log:
        stdout = "logs/run.o",
        stderr = "logs/run.e"
    shell:
        " python2 {input} -j {threads} -g {params.mem} --quiet "
        " 1>{log.stdout} 2>{log.stderr} "


rule combine:
    input:
        diploid = "rundir/results/variants/diploidSVconverted.vcf.gz"
    output:
        "manta.vcf.gz"
    params:
        resultdir = "rundir/results"
    shell:
        "../manta_combine.sh {params.resultdir} | bgzip -c > {output}"

rule splitvcf:
    input:
        "manta.vcf.gz"
    output:
        variants = "manta_{svtype}.vcf.gz",
    shell:
        " bcftools view -i'INFO/SVTYPE=\"{wildcards.svtype}\" && INFO/CIEND!=\".\"' "
        "{input} -Oz -o {output.variants}"
